WHITESPACE = _{ " " }

program = _{
    SOI ~
    data_section? ~
    program_section ~
    EOI
}

data_section = _{
    "data:" ~ NEWLINE ~ data_list
}

data_list = _{
    (data_line? ~ NEWLINE)*
}

data_line = {
    identifier ~ string_value
}

_unsigned_number = @{
    DECIMAL_NUMBER+
}

_signed_number = @{
    "-"? ~ _unsigned_number
}

unsigned_number = @{
    _unsigned_number ~ "u"
}

signed_number = @{
    _signed_number ~ "i"
}

identifier = @{
    "." ~ (ASCII_ALPHANUMERIC | "_")+
}

string_value = @{
    "\"" ~ (ASCII_ALPHANUMERIC | " ")* ~ "\""
}

program_section = _{
    "program:" ~ NEWLINE ~ instructions_list
}

instructions_list = _{
    ((comment | program_line | mark)? ~ NEWLINE)*
}

mark = @{"@" ~ (ASCII_ALPHANUMERIC | "_")+}

comma = _{ "," }

program_line = {
    instruction ~ (instruction_arg ~ (comma ~ instruction_arg)*)?
}

instruction_arg = {
    (identifier | unsigned_number | signed_number | register | mark)
}

register = @{
    ASCII_ALPHA ~ NUMBER
}

instruction = @{
    ASCII_ALPHA+
}

comment = @{
    "//" ~ ASCII_CONTENT
}

ASCII_CONTENT = @{
    (ASCII_ALPHANUMERIC | SYMBOL | SEPARATOR | PUNCTUATION)*
}