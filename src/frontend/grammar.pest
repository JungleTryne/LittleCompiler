WHITESPACE = _{ " " }

program = _{
    SOI ~
    data_section? ~
    program_section ~
    EOI
}

data_section = _{
    "data:" ~ NEWLINE ~ data_list
}

data_list = _{
    (data_line? ~ NEWLINE)*
}

data_line = {
    identifier ~ string_value
}

number = @{
    "-"? ~ DECIMAL_NUMBER+
}

identifier = @{
    "." ~ (ASCII_ALPHANUMERIC | "_")+
}

string_value = @{
    "\"" ~ (ASCII_ALPHANUMERIC | " ")* ~ "\""
}

program_section = _{
    "program:" ~ NEWLINE ~ instructions_list
}

instructions_list = _{
    ((program_line | mark)? ~ NEWLINE)*
}

mark = @{"@" ~ (ASCII_ALPHANUMERIC | "_")+}

comma = _{ "," }

program_line = {
    instruction ~ (instruction_arg ~ (comma ~ instruction_arg)*)?
}

instruction_arg = {
    (identifier | number | register | mark)
}

register = @{
    ASCII_ALPHA ~ NUMBER
}

instruction = @{
    ASCII_ALPHA+
}
